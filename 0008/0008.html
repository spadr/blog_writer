<div id="toc"></div>
<p>Gravity Infrared Co2 SensorはDFRobot社製のCO2センサです。<br>
測定には非分散赤外線方式（NDIR）を採用しており、大気中の二酸化炭素濃度を0〜5000 ppmの範囲で測定します。測定値はDACで出力されるので、今回はM5StickC本体のADC(0-3.3V,12bit)で値を読み取ります。</p>
<h2>商品について</h2>
<ul>
<li>
<p><a href="https://www.switch-science.com/catalog/4026/">Gravity - 赤外線CO2センサ/メーター｜スイッチサイエンス</a></p>
</li>
<li>
<p><a href="https://wiki.dfrobot.com/Gravity__Analog_Infrared_CO2_Sensor_For_Arduino_SKU__SEN0219">Gravity Analog Infrared CO2 Sensor For Arduino SKU SEN0219 | DFRobotWiki </a></p>
</li>
<li>
<p><a href="https://www.dfrobot.com/blog-991.html">ESP32 Arduino: Using an infrared CO2 sensor | DFRobotチュートリアル</a></p>
</li>
</ul>
<p><img src="/media/post_content_images/0008_03.jpg" alt="alt"></p>
<p>表側です</p>
<p><img src="/media/post_content_images/0008_04.jpg" alt="alt"></p>
<p>裏側です</p>
<p><img src="/media/post_content_images/0008_05.jpg" alt="alt"></p>
<p>横からです</p>
<h2>サンプルコード</h2>
<ul>
<li><a href="https://github.com/spadr/M5/blob/main/M5StickC/Gravity_infrared_co2_sensor/Gravity_infrared_co2_sensor.ino">https://github.com/spadr/M5/blob/main/M5StickC/Gravity_infrared_co2_sensor/Gravity_infrared_co2_sensor.ino</a></li>
</ul>
<pre class="prettyprint lang-c">
    #include &lt;M5StickC.h&gt;

const int PIN = 36;

void setup(void)
{
    M5.begin();
    Serial.begin(115200);
    pinMode(PIN, INPUT);

    M5.Lcd.fillScreen(BLACK);
    M5.Lcd.setTextColor(ORANGE);
}

void loop(void)
{
    Serial.println(&quot;Getting Concentration from infrared CO2 sensor&quot;);
    Serial.println(&quot; &quot;);
    
    float vol = (analogRead(PIN) + 1) * 3.3 / (4095 + 1);//0-3.3V,12bit(0-4095)
    float conc  = 2900.63 * vol - 801.26;//Liner equation through P and Q. P(0.42v,417ppm), Q(2.0v,5000ppm)
                                         //CO2 Concentration in Japan in 2020 is 417 ppm.
    
    Serial.print(&quot;CO2 Concentration : &quot;);
    Serial.println(conc);
    M5.Lcd.fillScreen(BLACK);

    String data = String(conc ,0);
    M5.Lcd.drawCentreString(&quot;CO2 Conc &quot;, 0, 10, 2);
    M5.Lcd.drawCentreString(data, 0, 30, 4);
    M5.Lcd.drawCentreString(&quot;ppm&quot;, 60, 30, 2);

    Serial.println(&quot; &quot;);
    Serial.println(&quot;        ***************************        &quot;);
    Serial.println(&quot; &quot;);

    delay(1000);
}
</pre>
<p>単純にanalogRead()で電圧を読むだけなので、難しくありません。</p>
<h2>CO2濃度の換算式について</h2>
<pre class="prettyprint lang-c">
    //0-3.3V,12bit(0-4095)
float vol = (analogRead(PIN) + 1) * 3.3 / (4095 + 1);

//CO2 Concentration in Japan in 2020 is 417 ppm.
//Liner equation through P and Q. P(0.42v,417ppm), Q(2.0v,5000ppm)
float conc  = 2900.63 * vol - 801.26;
</pre>
<p>電圧からCO2濃度への換算式は<a href="https://keisan.casio.jp/exec/system/1173752940">２点を通る直線の方程式 - 高精度計算サイト</a>で作成しました。</p>
<p><a href="https://wiki.dfrobot.com/Gravity__Analog_Infrared_CO2_Sensor_For_Arduino_SKU__SEN0219">公式Wiki</a>
の図によると0.4Vの出力電圧があるときは0ppmと読み取れるため、最初はP(0.4v,0ppm), Q(2.0v,5000ppm)の条件で測定を行いましたが、それらしい値を得られませんでした。</p>
<p>そこで、実際に一晩屋外で測定したところ、出力電圧は平均して0.42Vでした。また、昨年の<a href="https://ds.data.jma.go.jp/ghg/kanshi/ghgp/co2_trend.html">日本国内のCO2濃度</a>は417ppmだったようなのでそれらを加味して、換算式はCO2濃度 = 2900.63 * 出力電圧 - 801.26としました。</p>
<p>さらに厳密にする場合はCO2の標準ガスをつかうのですが、このくらいの精度で十分だと思います。</p>
<h2>M5StickCでの使い方</h2>
<p>1.下記のURLからサンプルコードをダウンロードする</p>
<ul>
<li><a href="https://github.com/spadr/M5">https://github.com/spadr/M5</a></li>
</ul>
<p><img src="/media/post_content_images/0006_05.jpg" alt="alt"></p>
<p>写真上部の矢印が指す&quot;Code&quot;をクリックしてメニューを出し、その中の&quot;Download ZIP&quot;をクリックするとダウンロード出来ます。ZIPファイルは任意のフォルダーにダウンロードして解凍して下さい。</p>
<p>2.サンプルコードをM5StickCに書き込む<br>
上の手順で解凍されたフォルダー内の&quot;M5StickC/Gravity_infrared_co2_sensor/Gravity_infrared_co2_sensor.ino&quot;をArduinoIDE等で開き、M5StickCに書き込みます。</p>
<p>3.G36,5V,GNDにリード線を接続する</p>
<p><img src="/media/post_content_images/0008_06.jpg" alt="alt"></p>
<p>写真のようにリード線を接続してください。</p>
<h2>CANASPAD-IoTで使う方法</h2>
<pre class="prettyprint lang-c">
#include &lt;WiFi.h&gt;
#include &lt;HTTPClient.h&gt;
#include &lt;M5StickC.h&gt;

const char SSID[] = &quot;Your WIFI SSID&quot;;
const char PASSWORD[] = &quot;Your WIFI PASS&quot;;
const char* host     = &quot;iot.canaspad.com&quot;;
const char* event    = &quot;/data/&quot;;
const char* device_name    = &quot;Your Device Name&quot;;
const char* access_key    = &quot;Your Access Key&quot;; //アカウントの新規登録のためにお送りした、アクティベーションメールに記載されています

const int PIN = 36;

void setup() {
  M5.begin();
  Serial.begin(115200);
  
  WiFi.begin(SSID, PASSWORD);
  Serial.print(&quot;WiFi connecting&quot;);
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(&quot;.&quot;);
    delay(100);
  }
  Serial.println(&quot; connected&quot;);
  
  pinMode(PIN, INPUT);
  
  M5.Lcd.fillScreen(BLACK);
  M5.Lcd.setTextColor(ORANGE);
}

void loop() {
  HTTPClient http;
  
  String URL = &quot;http://&quot;;
         URL += String(host);
         URL += String(event);
         URL += String(access_key)   + &quot;,&quot;;
         URL += String(device_name)  + &quot;,&quot;;
         URL += measure() + &quot;/&quot;;
  Serial.print(&quot;Requesting URL: &quot;);
  Serial.println(URL);
  
  http.begin(URL);
  int httpCode = http.GET();
  if (httpCode &gt; 0) {
    String payload = http.getString();
    Serial.print(&quot;httpCode : &quot;);
    Serial.println(httpCode);
    Serial.print(&quot;payload : &quot;);
    Serial.println(payload);
    }
  else {
      Serial.print(&quot;Error on HTTP request!&quot;);
      Serial.println(httpCode);
    }
  
  http.end();
  
  delay(1*60*1000);
}


String measure(){
    Serial.println(&quot;Getting Concentration from infrared CO2 sensor&quot;);
    Serial.println(&quot; &quot;);
    
    float vol = (analogRead(PIN) + 1) * 3.3 / (4095 + 1);//0-3.3V,12bit(0-4095)
    float conc  = 2900.63 * vol - 801.26;//Liner equation through P and Q. P(0.42v,417ppm), Q(2.0v,5000ppm)
                                         //CO2 Concentration in Japan in 2020 is 417 ppm.
    
    Serial.print(&quot;CO2 Concentration : &quot;);
    Serial.println(conc);
    M5.Lcd.fillScreen(BLACK);

    String data = String(conc ,0);
    M5.Lcd.drawCentreString(&quot;CO2 Conc &quot;, 0, 10, 2);
    M5.Lcd.drawCentreString(data, 0, 30, 4);
    M5.Lcd.drawCentreString(&quot;ppm&quot;, 65, 30, 2);

    Serial.println(&quot; &quot;);
    Serial.println(&quot;        ***************************        &quot;);
    Serial.println(&quot; &quot;);

    return String(conc);
}
</pre>
<p><img src="/media/post_content_images/0008_07.png" alt="alt"></p>
<ul>
<li><a href="https://github.com/spadr/CANASPAD-IoT_SAMPLE/tree/main/M5StickC/Gravity_infrared_co2_sensor">https://github.com/spadr/CANASPAD-IoT_SAMPLE/tree/main/M5StickC/Gravity_infrared_co2_sensor</a></li>
</ul>
<p>Githubにもサンプルコードを記載してます。</p>
<h2>応答性について</h2>
<p>サンプルコードをつかってCANASPAD-IoTにデータを送信して応答性を確認しました。
<img src="/media/post_content_images/0008_01.png" alt="alt"></p>
<p>若干ノイズが乗っていますが、電源投入後から徐々に400ppm付近へ収束しています。
最終的に大気のCO2濃度である400ppm付近になるまで2時間程かかりました。
実装する際は、応答に2時間かかることを留意する必要がありそうです。
また、ノイズについてはM5StickC本体のADCによるところも大きいかもしれません。
気になる場合は、別途ADCを接続して試すか、平均しても良さそうです。</p>
<h2>おわりに</h2>
<p>今回はCO2センサを題材にしてCANASPAD-IoTのチュートリアルを作成しました。</p>
<p>CANASPAD-IoTはマイコンを使ってIoTをプロトタイプしたり、センシングデータから相関ルールを探索するために便利なサービスです。無料なのでぜひご利用ください。</p>
