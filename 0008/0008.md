## Gravity infrared co2 sensor v1.1
Gravity Infrared Co2 SensorはDFRobot社製のCO2センサです。  
測定には非分散赤外線方式（NDIR）を採用しており、大気中の二酸化炭素濃度を0〜5000 ppmの範囲で測定します。測定値はDACで出力されるので、今回はM5StickC本体のADC(0-3.3V,12bit)で値を読み取ります。
```
<div id="toc"></div>
```
## 商品について  
- [Gravity - 赤外線CO2センサ/メーター｜スイッチサイエンス](https://www.switch-science.com/catalog/4026/)

- [Gravity Analog Infrared CO2 Sensor For Arduino SKU SEN0219 | DFRobotWiki ](https://wiki.dfrobot.com/Gravity__Analog_Infrared_CO2_Sensor_For_Arduino_SKU__SEN0219)

- [ESP32 Arduino: Using an infrared CO2 sensor | DFRobotチュートリアル](https://www.dfrobot.com/blog-991.html)

![alt](/media/post_content_images/0008_03.jpg)

表側です

![alt](/media/post_content_images/0008_04.jpg)

裏側です

![alt](/media/post_content_images/0008_05.jpg)

横からです  

## サンプルコード  

- [https://github.com/spadr/M5/blob/main/M5StickC/Gravity_infrared_co2_sensor/Gravity_infrared_co2_sensor.ino](https://github.com/spadr/M5/blob/main/M5StickC/Gravity_infrared_co2_sensor/Gravity_infrared_co2_sensor.ino)

```cpp
#include <M5StickC.h>

const int PIN = 36;

void setup(void)
{
    M5.begin();
    Serial.begin(115200);
    pinMode(PIN, INPUT);

    M5.Lcd.fillScreen(BLACK);
    M5.Lcd.setTextColor(ORANGE);
}

void loop(void)
{
    Serial.println("Getting Concentration from infrared CO2 sensor");
    Serial.println(" ");
    
    float vol = (analogRead(PIN) + 1) * 3.3 / (4095 + 1);//0-3.3V,12bit(0-4095)
    float conc  = 2900.63 * vol - 801.26;//Liner equation through P and Q. P(0.42v,417ppm), Q(2.0v,5000ppm)
                                         //CO2 Concentration in Japan in 2020 is 417 ppm.
    
    Serial.print("CO2 Concentration : ");
    Serial.println(conc);
    M5.Lcd.fillScreen(BLACK);

    String data = String(conc ,0);
    M5.Lcd.drawCentreString("CO2 Conc ", 0, 10, 2);
    M5.Lcd.drawCentreString(data, 0, 30, 4);
    M5.Lcd.drawCentreString("ppm", 60, 30, 2);

    Serial.println(" ");
    Serial.println("        ***************************        ");
    Serial.println(" ");

    delay(1000);
}
```

単純にanalogRead()で電圧を読むだけなので、難しくありません。  
    


## CO2濃度の換算式について

```cpp
//0-3.3V,12bit(0-4095)
float vol = (analogRead(PIN) + 1) * 3.3 / (4095 + 1);

//CO2 Concentration in Japan in 2020 is 417 ppm.
//Liner equation through P and Q. P(0.42v,417ppm), Q(2.0v,5000ppm)
float conc  = 2900.63 * vol - 801.26;
```
電圧からCO2濃度への換算式は[２点を通る直線の方程式 - 高精度計算サイト](https://keisan.casio.jp/exec/system/1173752940)で作成しました。

[公式Wiki](https://wiki.dfrobot.com/Gravity__Analog_Infrared_CO2_Sensor_For_Arduino_SKU__SEN0219)
の図によると0.4Vの出力電圧があるときは0ppmと読み取れるため、最初はP(0.4v,0ppm), Q(2.0v,5000ppm)の条件で測定を行いましたが、それらしい値を得られませんでした。

そこで、実際に一晩屋外で測定したところ、出力電圧は平均して0.42Vでした。また、昨年の[日本国内のCO2濃度](https://ds.data.jma.go.jp/ghg/kanshi/ghgp/co2_trend.html)は417ppmだったようなのでそれらを加味して、換算式はCO2濃度 = 2900.63 * 出力電圧 - 801.26としました。

さらに厳密にする場合はCO2の標準ガスをつかうのですが、このくらいの精度で十分だと思います。

## M5StickCでの使い方  
1.下記のURLからサンプルコードをダウンロードする  
- [https://github.com/spadr/M5](https://github.com/spadr/M5)

![alt](/media/post_content_images/0006_05.jpg)

写真上部の矢印が指す"Code"をクリックしてメニューを出し、その中の"Download ZIP"をクリックするとダウンロード出来ます。ZIPファイルは任意のフォルダーにダウンロードして解凍して下さい。

2.サンプルコードをM5StickCに書き込む  
上の手順で解凍されたフォルダー内の"M5StickC/Gravity_infrared_co2_sensor/Gravity_infrared_co2_sensor.ino"をArduinoIDE等で開き、M5StickCに書き込みます。

3.G36,5V,GNDにリード線を接続する  

![alt](/media/post_content_images/0008_06.jpg)

写真のようにリード線を接続してください。

## CANASPAD-IoTで使う方法 

```cpp
<pre class="prettyprint lang-c">
#include <WiFi.h>
#include <HTTPClient.h>
#include <M5StickC.h>

const char SSID[] = "Your WIFI SSID";
const char PASSWORD[] = "Your WIFI PASS";
const char* host     = "iot.canaspad.com";
const char* event    = "/data/";
const char* device_name    = "Your Device Name";
const char* access_key    = "Your Access Key"; //アカウントの新規登録のためにお送りした、アクティベーションメールに記載されています

const int PIN = 36;

void setup() {
  M5.begin();
  Serial.begin(115200);
  
  WiFi.begin(SSID, PASSWORD);
  Serial.print("WiFi connecting");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(100);
  }
  Serial.println(" connected");
  
  pinMode(PIN, INPUT);
  
  M5.Lcd.fillScreen(BLACK);
  M5.Lcd.setTextColor(ORANGE);
}

void loop() {
  HTTPClient http;
  
  String URL = "http://";
         URL += String(host);
         URL += String(event);
         URL += String(access_key)   + ",";
         URL += String(device_name)  + ",";
         URL += measure() + "/";
  Serial.print("Requesting URL: ");
  Serial.println(URL);
  
  http.begin(URL);
  int httpCode = http.GET();
  if (httpCode > 0) {
    String payload = http.getString();
    Serial.print("httpCode : ");
    Serial.println(httpCode);
    Serial.print("payload : ");
    Serial.println(payload);
    }
  else {
      Serial.print("Error on HTTP request!");
      Serial.println(httpCode);
    }
  
  http.end();
  
  delay(1*60*1000);
}


String measure(){
    Serial.println("Getting Concentration from infrared CO2 sensor");
    Serial.println(" ");
    
    float vol = (analogRead(PIN) + 1) * 3.3 / (4095 + 1);//0-3.3V,12bit(0-4095)
    float conc  = 2900.63 * vol - 801.26;//Liner equation through P and Q. P(0.42v,417ppm), Q(2.0v,5000ppm)
                                         //CO2 Concentration in Japan in 2020 is 417 ppm.
    
    Serial.print("CO2 Concentration : ");
    Serial.println(conc);
    M5.Lcd.fillScreen(BLACK);

    String data = String(conc ,0);
    M5.Lcd.drawCentreString("CO2 Conc ", 0, 10, 2);
    M5.Lcd.drawCentreString(data, 0, 30, 4);
    M5.Lcd.drawCentreString("ppm", 65, 30, 2);

    Serial.println(" ");
    Serial.println("        ***************************        ");
    Serial.println(" ");

    return String(conc);
}
</pre>
```

![alt](/media/post_content_images/0008_07.png)

- [https://github.com/spadr/CANASPAD-IoT_SAMPLE/tree/main/M5StickC/Gravity_infrared_co2_sensor](https://github.com/spadr/CANASPAD-IoT_SAMPLE/tree/main/M5StickC/Gravity_infrared_co2_sensor)

Githubにもサンプルコードを記載してます。


## 応答性について
サンプルコードをつかってCANASPAD-IoTにデータを送信して応答性を確認しました。
![alt](/media/post_content_images/0008_01.png)

若干ノイズが乗っていますが、電源投入後から徐々に400ppm付近へ収束しています。
最終的に大気のCO2濃度である400ppm付近になるまで2時間程かかりました。
実装する際は、応答に2時間かかることを留意する必要がありそうです。
また、ノイズについてはM5StickC本体のADCによるところも大きいかもしれません。
気になる場合は、別途ADCを接続して試すか、平均しても良さそうです。


## おわりに
今回はCO2センサを題材にしてCANASPAD-IoTのチュートリアルを作成しました。

CANASPAD-IoTはマイコンを使ってIoTをプロトタイプしたり、センシングデータから相関ルールを探索するために便利なサービスです。無料なのでぜひご利用ください。
